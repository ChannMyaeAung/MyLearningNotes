# Changing the Data Record Layout

- If we hard-coded offsets instead of using constants, we would have to go and find these offsets and rewrite them based on the new record format.
- Below, we will be adding a person's shoe size offset as the second field in the record.

`persondataexpanded.s`

```assembly
.section .data
.globl people, numpeople
numpeople:
    # Calculate the num of ppl in array
    .quad (endpeople - people) / PERSON_RECORD_SIZE

people:
    # Array of people
    .quad 200, 10, 2, 74, 20
    .quad 280, 9, 2, 72, 44
    .quad 150, 8, 1, 68, 30
    .quad 250, 7, 2, 75, 24
    .quad 180, 6, 3, 70, 35
endpeople:

.globl WEIGHT_OFFSET, SHOE_OFFSET, HAIR_OFFSET, HEIGHT_OFFSET, AGE_OFFSET
.equ WEIGHT_OFFSET, 0
.equ SHOE_OFFSET, 8
.equ HAIR_OFFSET, 16
.equ HEIGHT_OFFSET, 24
.equ AGE_OFFSET, 32

.globl PERSON_RECORD_SIZE
.equ PERSON_RECORD_SIZE, 40 (8 bytes * 5)
```



`tallest.s`

```assembly
.globl _start
.section .text
_start:
    # Pointer to the 1st record 
    leaq people, %rbx

    # Total record count
    movq numpeople, %rcx

    # Tallest person
    movq $0, %rdi

    # if there are no records, exit
    cmpq $0, %rcx
    je exit

mainloop:
    # %rbx is the pointer to the whole struct
    # this instruction grabs the height field and stores it in %rax
    movq HEIGHT_OFFSET(%rbx), %rax

    # If it is <= the current tallest person, skip
    cmpq %rdi, %rax
    jbe endloop

    # otherwise, store this value as the tallest
    movq %rax, %rdi

endloop:
    # Move %rbx to point to the next record
    addq $PERSON_RECORD_SIZE, %rbx

    # decrement %rcx and loop if it is not 0
    loopq mainloop

exit:
    movq $60, %rax
    syscall
```

`browncount.s`

```assembly
.globl _start
.section .text
_start:
    # Pointer to the 1st record 
    leaq people, %rbx

    # Total record count
    movq numpeople, %rcx

    # Brown hair count
    movq $0, %rdi

    # if there are no records, exit
    cmpq $0, %rcx
    je exit

mainloop:
    # Is the hair brown (2)?
    cmpq $2, HAIR_OFFSET(%rbx)
    # No? Go to the next record
    jne endloop

    # Yes? Increment the count
    incq %rdi

endloop:
    addq $PERSON_RECORD_SIZE, %rbx
    loopq mainloop

exit:
    movq $60, %rax
    syscall
```

`bigshoecount.s`

```assembly
.globl _start
.section .text
_start:
    # Pointer to the 1st record
    leaq people, %rbx

    # Total record count
    movq numpeople, %rcx

    # Largest shoe size
    movq $0, %rdi

    # If there are no records, exit
    cmpq $0, %rcx
    je exit

mainloop:
    # % rbx is the pointer to the whole struct
    # grabs the shoe size field and stores it in % rax
    movq SHOE_OFFSET(%rbx), %rax

    # if it is <= the current largest shoe size, skip
    cmpq %rdi, %rax
    jbe endloop # jump if below or equal

    # otherwise, store this value as the largest
    movq %rax, %rdi

endloop:
    # Move %rbx to point to the next record
    addq $PERSON_RECORD_SIZE, %rbx

    # decrement %rcx and loop if it is not 0
    loopq mainloop

exit:
    movq $60, %rax
    syscall
```



**Running tallest program**

```sh
chan@CMA:~/C_Programming/Assembly$ as persondataexpanded.s -o persondataexpanded.o
persondataexpanded.s: Assembler messages:
persondataexpanded.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ as tallest.s -o tallest.o
tallest.s: Assembler messages:
tallest.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld persondataexpanded.o tallest.o -o tallest
chan@CMA:~/C_Programming/Assembly$ ./tallest
chan@CMA:~/C_Programming/Assembly$ echo $?
75

```

**Running browncount program**

```sh
chan@CMA:~/C_Programming/Assembly$ as browncount.s -o browncount.o
browncount.s: Assembler messages:
browncount.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld browncount.o persondataexpanded.o -o browncount
chan@CMA:~/C_Programming/Assembly$ ./browncount
chan@CMA:~/C_Programming/Assembly$ echo $?
3
```

**Running bigshoecount program**

```sh
chan@CMA:~/C_Programming/Assembly$ as bigshoecount.s -o bigshoecount.o
bigshoecount.s: Assembler messages:
bigshoecount.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld bigshoecount.o persondataexpanded.o -o bigshoecount
chan@CMA:~/C_Programming/Assembly$ ./bigshoecount
chan@CMA:~/C_Programming/Assembly$ echo $?
10
```

