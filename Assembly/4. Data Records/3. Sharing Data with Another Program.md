# Sharing Data With Another Program

- This program will count the number of brown-haired people (HAIR_OFFSET = 2) in our data.

`test.s`

```assembly
.section .data
.globl people, numpeople
numpeople:
    # Calculate the num of ppl in array
    .quad (endpeople - people) / PERSON_RECORD_SIZE

people:
    # Array of people
    .quad 200, 2, 74, 20
    .quad 280, 2, 72, 44
    .quad 150, 1, 68, 30
    .quad 250, 2, 75, 24
    .quad 180, 3, 70, 35
endpeople:

.globl WEIGHT_OFFSET, HAIR_OFFSET, HEIGHT_OFFSET, AGE_OFFSET
.equ WEIGHT_OFFSET, 0
.equ HAIR_OFFSET, 8
.equ HEIGHT_OFFSET, 16
.equ AGE_OFFSET, 24

.globl PERSON_RECORD_SIZE
.equ PERSON_RECORD_SIZE, 32
```

`test2.s`

```assembly
.globl _start
.section .text
_start:
    # Pointer to the 1st record 
    leaq people, %rbx

    # Total record count
    movq numpeople, %rcx

    # Brown hair count
    movq $0, %rdi

    # if there are no records, exit
    cmpq $0, %rcx
    je exit

mainloop:
    # Is the hair brown (2)?
    cmpq $2, HAIR_OFFSET(%rbx)
    # No? Go to the next record
    jne endloop

    # Yes? Increment the count
    incq %rdi

endloop:
    addq $PERSON_RECORD_SIZE, %rbx
    loopq mainloop

exit:
    movq $60, %rax
    syscall
```



```sh
chan@CMA:~/C_Programming/Assembly$ as test.s -o test.o
test.s: Assembler messages:
test.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ as test2.s -o test2.o
test2.s: Assembler messages:
test2.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld test.o test2.o -o test2
chan@CMA:~/C_Programming/Assembly$ ./test2
chan@CMA:~/C_Programming/Assembly$ echo $?
3
```

