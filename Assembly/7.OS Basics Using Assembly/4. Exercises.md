# Exercises

1. Write a program that prints out two different strings one right after the other.

```assembly
.globl _start 
.section .data 
str1:
	.ascii "Maliketh, "
str2: 
	.ascii "The Black Blade!\n"

.section .text 
_start: 
	# print first string ("Maliketh")
	mov $1, %rax # sys_write 
	mov $1, %rdi # stdout 
	lea str1(%rip), %rsi # address of str1

	# num of bytes to write
	mov $10, %rdx # length: 10 bytes 
	syscall 

	# print second string ("Marika!\n")
	mov $1, %rax # sys_write
	mov $1, %rdi # stdout
	lea str2(%rip), %rsi # address of str2
	mov $17, %rdx # length: 17 bytes
	syscall 

	# exit
	mov $60, %rax 
	xor %rdi, %rdi
	syscall 
```

```sh
chan@CMA:~/C_Programming/Assembly$ as practice.s -o practice.o
practice.s: Assembler messages:
practice.s:26: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld practice.o -o practice
chan@CMA:~/C_Programming/Assembly$ ./practice
Maliketh, The Black Blade! 
chan@CMA:~/C_Programming/Assembly$ 
```

---

2. Write a program that prints out the same string ten times in a loop.

```assembly
.section .data
msg:
	.asciz "Hello, world!\n"   

.section .text
.globl _start
_start:
	# Use R12 as the loop counter (10 iterations)
	# if we use rcx as the loop counter it will be overwritten during each syscall (will result in infinite loop)
	# r12 is not modified by syscall.
	mov $10, %r12

.loop:
	# Set up and perform sys_write to print the string.
	lea msg(%rip), %rsi   # pointer to the string
	mov $1, %rdi          # file descriptor: stdout
	mov $14, %rdx         # length of the string (14 bytes)
	mov $1, %rax          # sys_write syscall number
	syscall

	dec %r12              # decrement loop counter
	jnz .loop             # if not zero, loop again

	# Exit the program.
	mov $60, %rax         # sys_exit syscall number
	xor %rdi, %rdi        # exit code 0
	syscall
```

- **Recall:** When we invoke the sys_write system call, the kernel expects several parameters in specific registers:
  - **%rdi:** File descriptor (for example, 1 for stdout).
  - **%rsi:** Address of the buffer containing the data to write.
  - **%rdx:** The number of bytes to write from that buffer.

```sh
chan@CMA:~/C_Programming/Assembly$ as loop.s -o loop.o
chan@CMA:~/C_Programming/Assembly$ ld loop.o -o loop
chan@CMA:~/C_Programming/Assembly$ ./loop
Hello, world!
Hello, world!
Hello, world!
Hello, world!
Hello, world!
Hello, world!
Hello, world!
Hello, world!
Hello, world!
Hello, world!
chan@CMA:~/C_Programming/Assembly$ 
```

---

3. Write a program that takes a number stored in memory and determines if that number is odd or even. Rather than using the exit status to communicate this information, have the program choose between two different strings to write to standard output.

```assembly
.section .data 
number: .quad 123
even_msg: .asciz "Even\n"
odd_msg: .asciz "Odd\n"

.section .text 
.globl _start 
_start: 
    # load the number from memory into %rax
    mov number, %rax 

    # test the LSB (bit 0)
    and $1, %rax # %rax now becomes 0 if even, 1 if odd 

    # compare with 0.
    cmp $0, %rax
    je print_even 

print_odd:
    mov $1, %rdi # stdout
    mov $odd_msg, %rsi # address of odd_msg
    mov $4, %rdx # length of odd_msg
    mov $1, %rax # sys_write
    syscall 
    jmp exit 

print_even:
    mov $1, %rdi # stdout
    mov $even_msg, %rsi # address of even_msg
    mov $5, %rdx # length of even_msg 
    mov $1, %rax # sys_write
    syscall
exit:
    mov $60, %rax 
    xor %rdi, %rdi
    syscall
```

```sh
chan@CMA:~/C_Programming/Assembly$ as assembly.s -o assembly.o
chan@CMA:~/C_Programming/Assembly$ ld assembly.o -o assembly
chan@CMA:~/C_Programming/Assembly$ ./assembly
Odd
chan@CMA:~/C_Programming/Assembly$ as assembly.s -o assembly.o
chan@CMA:~/C_Programming/Assembly$ ld assembly.o -o assembly
chan@CMA:~/C_Programming/Assembly$ ./assembly
Even
```

---

4. Write a program that loops ten times and alternates between printing two different strings each time.

```assembly
	.section .data
msg1:
	.asciz "Maliketh\n"      # 9 bytes including newline
msg2:
	.asciz "The Black Blade!\n"      # 17 bytes including newline

	.section .text
	.globl _start
_start:
	# Initialize loop counter in r12 (starting at 0)
	xor %r12, %r12

.loop:
	# If counter >= 10, exit the loop.
	cmp $10, %r12
	jge .exit

	# Check the lowest bit to determine even/odd.
	test $1, %r12
	je .print_msg1          # if even, jump to print msg1

.print_msg2:
	# Print msg2 (odd iterations)
	mov $1, %rdi            # stdout file descriptor
	lea msg2(%rip), %rsi    # pointer to msg2
	mov $17, %rdx           # length of msg2 
	mov $1, %rax            # sys_write 
	syscall
	jmp .inc                # increment loop counter and repeat

.print_msg1:
	# Print msg1 (even iterations)
	mov $1, %rdi            # stdout file descriptor
	lea msg1(%rip), %rsi    # pointer to msg1
	mov $9, %rdx           # length of msg1 
	mov $1, %rax            # sys_writ
	syscall

.inc:
	inc %r12                # Increment loop counter
	jmp .loop

.exit:
	mov $60, %rax           
	xor %rdi, %rdi          
	syscall
```

```sh
chan@CMA:~/C_Programming/Assembly$ as assembly.s -o assembly.o
chan@CMA:~/C_Programming/Assembly$ ld assembly.o -o assembly
chan@CMA:~/C_Programming/Assembly$ ./assembly
Maliketh
The Black Blade!
Maliketh
The Black Blade!
Maliketh
The Black Blade!
Maliketh
The Black Blade!
Maliketh
The Black Blade!
chan@CMA:~/C_Programming/Assembly$ 
```

