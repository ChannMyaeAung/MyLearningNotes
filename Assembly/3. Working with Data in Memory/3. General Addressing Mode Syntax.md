# General Addressing Mode Syntax

`displacement(base, index, scale)`

`VALUE(BASEREG, IDXREG, MULTIPLIER)`

- **displacement (VALUE):**
  - An immediate constant (an offset) that is added to the address. 
  - It can be positive or negative and is optional if zero.
- **base (BASEREG):**
  - A base register whose value is added to the displacement. 
  - This register points to the start of a data structure or array.
  - If omitted, it is assumed to be zero.
- **index (IDXREG):**
  - An index register whose value is multiplied by the scale factor and then added to the sum of the displacement and base. 
  - Often used to index into arrays. It is optional.
- **scale (MULTIPLIER):**
  - A multiplier for the index register. 
  - Valid values are usually 1, 2, 4, or 8. 
  - If the index is omitted, the scale is ignored. 
  - If not specified, the default is 1.



The effective memory address that this refers to is calculated:

`EA = disp + base + (index Ã— scale)`

`address = VALUE + BASEREG + IDXREG * MULTIPLIER`

---

## Code Example

- A variant of the program to find the largest value.
- However, this one will use %rbx as an index into the array

```assembly
.globl _start

.section .data
numberofelements:
    .quad 7

mynumbers:
    .quad 5, 10, 15, 20, 95, 40, 32

### This program will find the largest value in the array
.section .text
_start:
    ## Initialize Registers

    # Put the number of elements of the array in %rcx
    movq numberofelements, %rcx

    # Put the index of the first element in %rbx
    movq $0, %rbx

    # Use %rdi to hold the current high value
    movq $0, %rdi

    ## Check preconditions

    # if there are no numbers, stop
    cmp $0, %rcx
    je endloop

myloop:
    # Get the next value of mynumbers indexed by %rbx
    movq mynumbers(, %rbx, 8), %rax

    # if it is not bigger, go to the end of the loop skipped the next line until loopcontrol
    cmp %rdi, %rax
    jbe loopcontrol

    # otherwise, store this as the biggest element so far
    movq %rax, %rdi

loopcontrol:
    # Move %rbx to the next index
    incq %rbx

    # Decrement %rcx and keep going until %rcx is zero (as it is what loopq does)
    loopq myloop

endloop:
    movq $60, %rax
    syscall
```

- `movq mynumbers(, %rbx, 8), %rax`: 
  - Calculates the effective address as `mynumbers + %rbx * 8`
  - `mynumbers`: The base address of the array.
  - `%rbx`: The index register, which holds the current index of the element in the array.
  - `8`: The scale factor, which is used to multiply the index. 
  - Since each element in the array is 8 bytes (a 64-bit integer), the scale factor is 8.
  - Calculating the effective addressing allows us to access each element of the array `mynumbers` by incrementing the index in %rbx.
  - BASEREG is missing, so it is assumed to be zero.

```sh
chan@CMA:~/C_Programming/Assembly$ as test.s -o test.o
test.s: Assembler messages:
test.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld test.o -o test
chan@CMA:~/C_Programming/Assembly$ ./test
chan@CMA:~/C_Programming/Assembly$ echo $?
95
```

---

## Code Example #2

- This time we will went from the end of the array back to the beginning.

```assembly
.globl _start

.section .data
numberofelements:
    .quad 7

mynumbers:
    .quad 5, 10, 15, 20, 95, 40, 98, 32

### This program will find the largest value in the array
.section .text
_start:
    ## Initialize Registers

    # Put the number of elements of the array in %rcx
    movq numberofelements, %rcx

    # Use %rdi to hold the current high value
    movq $0, %rdi

    ## Check preconditions

    # if there are no numbers, stop
    cmp $0, %rcx
    je endloop

myloop:
    # Get the next value of mynumbers indexed by %rbx
    movq mynumbers - 8 (, %rcx, 8), %rax

    # if it is not bigger, go to the end of the loop skipped the next line until loopcontrol
    cmp %rdi, %rax
    jbe loopcontrol

    # otherwise, store this as the biggest element so far
    movq %rax, %rdi

loopcontrol:
    # Decrement %rcx and keep going until %rcx is zero (as it is what loopq does)
    loopq myloop

endloop:
    movq $60, %rax
    syscall
```

- When `%rcx` is initialized to the number of elements in the array (e.g., 7), the first element accessed is the last element of the array:
  - Effective address = `mynumbers - 8 + (7 * 8) = mynumbers + 48` (points to the 7th element).
- As `%rcx` is decremented in each iteration, the effective address moves backward through the array:
  - When `%rcx = 6`, effective address = `mynumbers - 8 + (6 * 8) = mynumbers + 40` (points to the 6th element).
  - When `%rcx = 5`, effective address = `mynumbers - 8 + (5 * 8) = mynumbers + 32` (points to the 5th element).
  - And so on, until `%rcx = 1`, which accesses the first element.
- The subtraction here is not done by the CPU when it runs, it is done at the time of assembly.

```sh
chan@CMA:~/C_Programming/Assembly$ as test.s -o test.o
test.s: Assembler messages:
test.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld test.o -o test
chan@CMA:~/C_Programming/Assembly$ ./test
chan@CMA:~/C_Programming/Assembly$ echo $?
98
```

---