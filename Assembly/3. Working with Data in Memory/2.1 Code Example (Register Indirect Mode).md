# Register Indirect Mode

```assembly
.globl _start
.section .data
numberofnumbers:
    .quad 7

# The data elements
mynumbers:
    .quad 5, 20, 33, 80, 52, 10, 1

### This program will find the largest value in the array

.section .text
_start:
    ## Initialize registers

    # Put the num of elements of the array in %rcx
    movq numberofnumbers, %rcx

    # Put the address of the first element in %rbx
    movq $mynumbers, %rbx

    # Use %rdi to hold the current high value
    movq $0, %rdi

    ## Check Preconditions
    # if there are no numbers, stop
    cmp $0, %rcx
    je endloop

myloop:
    # get the next value (currently pointed to by %rbx)
    movq (%rbx), %rax

    # If it is not bigger, go to the end of the loop, skipping moveq %rax, %rdi
    cmp %rdi, %rax
    jbe loopcontrol

    # otherwise, store this as the biggest element so far
    movq %rax, %rdi

loopcontrol:
    # change the address in %rbx to point to the next vlaue (by adding 8 (the size of a quadword))
    addq $8, %rbx

    # decrement %rcx and keep going until %rcx is zero 
    loopq myloop

endloop:
    movq $60, %rax
    syscall
```

- `movq numberofnumbers`: Using direct addressing mode.
- `movq $mynumbers, %rbx`: is prefixed with a dollar sign which means that the memory address itself is being used as an immediate-mode value. The address itself is being loaded into %rbx.
  - Whatever address `mynumbers` referred to, that address is now in %rbx.
  - We can use (%rbx) to refer to the contents of that memory address.
- Both of these reference memory locatons.

```sh
chan@CMA:~/C_Programming/Assembly$ as test.s -o test.o
test.s: Assembler messages:
test.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld test.o -o test
chan@CMA:~/C_Programming/Assembly$ ./test
chan@CMA:~/C_Programming/Assembly$ echo $?
80

```

