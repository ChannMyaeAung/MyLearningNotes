# Allocating Additional Memory

- **Recall**: The **program break** is the point at which memory addresses are no longer valid.
  - The program break can be moved, however, by requesting more memory from the OS.
- If there isn't free memory available in our process, `malloc` will ask the OS for more memory, and the OS will move the **program break** to allow for us to have a larger address space to work with.
- `free` doesn't actually give back the memory to the OS, but rather marks the memory as free so that a future `malloc` will be able to use it.

---

## Example Code

```assembly
.section .note.GNU-stack, "", @progbits
.globl main 
.section .data
scanformat:
    .ascii "%499[^\n]\0"  # Read up to 499 characters, stopping at a newline

outformat:
    .ascii "%s\n\0"

.section .text
.equ LOCAL_BUFFER, -8 

main:
    # allocate one local var 
    enter $16, $0 

    # get the memory and store it in our local var
    movq $500, %rdi 
    call malloc 
    movq %rax, LOCAL_BUFFER(%rbp)

    # read the data from stdin 
    movq stdin, %rdi
    lea scanformat(%rip), %rsi 
    movq LOCAL_BUFFER(%rbp), %rdx
    movq $0, %rax
    call fscanf 

    # write the data to stdout
    movq stdout, %rdi
    lea outformat(%rip), %rsi
    movq LOCAL_BUFFER(%rbp), %rdx
    movq $0, %rax 
    call fprintf 

    # free the buffer
    movq LOCAL_BUFFER(%rbp), %rdi
    call free 

    # return
    movq $0, %rax
    leave 
    ret
```

- If we don't specify a number, if the user types more characters than we have allocated, it will just keep adding characters to the buffer, even if it goes beyond the end of the buffer. (**Buffer overflow**)
- If the buffer was allocated on the stack instead of the heap, it is **stack overflow**.

```sh
chan@CMA:~/C_Programming/Assembly$ ./run
Malloc Demo!
Malloc Demo!
chan@CMA:~/C_Programming/Assembly$ ./run
Hello, World!
Hello, World!
```

