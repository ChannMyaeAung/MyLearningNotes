# Reading data from a file

- Data can be read from a file using `fscanf`.

`test.s`

```assembly
.section .note.GNU-stack, "", @progbits
.globl main

.extern stdout
.extern stdin
.extern fprintf
.extern fscanf         

.section .data 
promptformat:
    .asciz "Enter two numbers separated by spaces, then press return.\n"
scanformat:
    .asciz "%ld %ld\0"        # %ld for 64-bit integers
resultformat:
    .asciz "The result is %ld.\n\0"  

.section .text 
.equ LOCAL_NUM, -8         # First local 8-byte slot at -8(%rbp)
.equ LOCAL_EXPONENT, -16   # Second local 8-byte slot at -16(%rbp)
main:
    enter $16, $0           # Allocate 16 bytes on the stack for two 8-byte variables

    # Print the prompt to stdout.
    movq stdout, %rdi       # 1st argument: stdout file pointer
    movq $promptformat, %rsi # 2nd argument: prompt string
    movq $0, %rax           # Clear %rax for variadic functions
    call fprintf

    # Read two numbers from standard input.
    movq stdin, %rdi                # 1st argument: stdin file pointer
    movq $scanformat, %rsi          # 2nd argument: format string ("%ld %ld")
    leaq LOCAL_NUM(%rbp), %rdx      # 3rd argument: address of first variable
    leaq LOCAL_EXPONENT(%rbp), %rcx # 4th argument: address of second variable
    movq $0, %rax                   # Clear %rax for variadic functions
    call fscanf                   

    # Call the exponent function:
    # Pass the first number as base (in %rdi) and the second as exponent (in %rsi).
    movq LOCAL_NUM(%rbp), %rdi      # Base
    movq LOCAL_EXPONENT(%rbp), %rsi # Exponent
    call exponent

    # Print the result returned in %rax.
    movq stdout, %rdi               # 1st argument: stdout file pointer
    movq $resultformat, %rsi        # 2nd argument: result format string
    movq %rax, %rdx                 # 3rd argument: result (from exponent)
    movq $0, %rax                   # Clear %rax for variadic functions
    call fprintf

    leave 
    ret
```

`exponentfunc.s`

```assembly
.section .note.GNU-stack, "", @progbits
.globl exponent         # Mark the function as globally accessible
.type exponent, @function

.section .text 
exponent:
    # %rdi: base, %rsi: exponent
    enter $16, $0         # Allocate 16 bytes on the stack for local data
    movq $1, %rax         # Initialize result to 1
    movq %rsi, -8(%rbp)   # Store the exponent in the local variable at -8(%rbp)

mainloop:
    mulq %rdi            # Multiply %rax by base (%rdi)
    decq -8(%rbp)        # Decrement the local exponent variable
    jnz mainloop         # Repeat if not zero

    leave                # Restore previous stack frame
    ret
```



```sh
chan@CMA:~/C_Programming/Assembly$ ./test
Enter two numbers separated by spaces, then press return.
2 3
The result is 8.
```

