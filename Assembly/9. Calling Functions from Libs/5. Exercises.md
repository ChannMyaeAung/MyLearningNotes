# Exercises

1. Modify one of the programs that prompt the user for input so that, after displaying the answer, it goes back and starts the process all over. Note that pressing Ctrl+C will terminate the program if you need to.

`factorialfunc.s`

```assembly
.section .note.GNU-stack, "", @progbits
.globl factorial
.section .text

# this is the offset into the stack frame (%rbp) that we store the number for which we are taking the factorial.
.equ LOCAL_NUM, -8

factorial:
    # we will reserve space for 1 variable - the value we were called with
    # aligned to 16 bytes
    enter $16, $0

    # if the arg is 1, then return the result as 1.
    # otherwise, continue 
    cmpq $1, %rdi
    jne continue

    # return 1
    movq $1, %rax
    leave 
    ret 

continue:
    # save the arg into our stack storage
    movq %rdi, LOCAL_NUM(%rbp)

    # call factorial with %rdi decreased by one
    decq %rdi
    call factorial 

    # the result will be in %rax. Multiply the result by our first arg we stored on the stack
    mulq LOCAL_NUM(%rbp)

    # result is in %rax which is what is needed for the return value

    leave 
    ret 
```

`test.s`

```assembly
.section .note.GNU-stack, "", @progbits
.globl main

.extern printf
.extern scanf

.section .data
prompt:         .asciz "Enter a number: "      # Prompt message
format_input:   .asciz "%d"                    # scanf format string
format_output:  .asciz "Factorial: %ld\n"        # printf format string (using %ld for a long)
input_value:    .long 0                        # Storage for the input number

.section .text
main:
    push %rbp
    mov %rsp, %rbp

.loop:
    # display the prompt
    lea prompt(%rip), %rdi
    call printf 

    # read an integer from stdin
    lea format_input(%rip), %rdi # 1st arg
    lea input_value(%rip), %rsi # 2nd arg : address to store input
    call scanf 

    # compute the factorial
    mov input_value(%rip), %rdi # move the input number into %rdi
    call factorial 

    # display the result
    lea format_output(%rip), %rdi # output format string
    mov %rax, %rsi # factorial result
    call printf 

    # loop back to prompt again
    jmp .loop
    pop %rbp
    ret 
```

```sh
chan@CMA:~/C_Programming/Assembly$ gcc test.s factorialfunc.s -o test
test.s: Assembler messages:
test.s:39: Warning: end of file not at end of a line; newline inserted
factorialfunc.s: Assembler messages:
factorialfunc.s:36: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ./test
Enter a number: 4
Factorial: 24
Enter a number: 5
Factorial: 120
Enter a number: 3
Factorial: 6
Enter a number: ^C
chan@CMA:~/C_Programming/Assembly$ 
```

---

2. The code to scan a single character is %c, which takes a pointer to a single byte (though you can also send it a pointer to a larger value than this if it is easier). Modify the program to ask the user to type Y to keep going.

`exponentfunc.s`

```assembly
.section .note.GNU-stack, "", @progbits
.globl exponent         # Mark the function as globally accessible
.type exponent, @function

.section .text 
exponent:
    # %rdi: base, %rsi: exponent
    enter $16, $0         # Allocate 16 bytes on the stack for local data
    movq $1, %rax         # Initialize result to 1
    movq %rsi, -8(%rbp)   # Store the exponent in the local variable at -8(%rbp)

mainloop:
    mulq %rdi            # Multiply %rax by base (%rdi)
    decq -8(%rbp)        # Decrement the local exponent variable
    jnz mainloop         # Repeat if not zero

    leave                # Restore previous stack frame
    ret
```

`test.s`

```assembly
.section .note.GNU-stack, "", @progbits
.globl main
.type main, @function

.extern printf
.extern scanf
.extern fscanf   

.section .data 
prompt:          .string "Enter a number: "      
format_input:    .string "%ld"                    
format_output:   .string "Exponent: %ld\n"        

continue_prompt: .string "Type Y to keep going: "
format_char:     .string " %c"                   # Leading space to skip whitespace

.section .bss
.lcomm input_value, 8      # Reserve 8 bytes for the input number (64-bit)
.lcomm continue_flag, 1    # Reserve 1 byte for the scanned character

.section .text 
.equ LOCAL_NUM, -8         # Local variable offset for main (if needed)
.equ LOCAL_EXPONENT, -16  

main:
    # Set up the stack frame for main (16 bytes for local variables)
    enter $16, $0

.loop:
    # Display the prompt
    lea prompt(%rip), %rdi
    call printf 

    # Read an integer from stdin (64-bit value)
    lea format_input(%rip), %rdi      # Format string "%ld"
    lea input_value(%rip), %rsi       # Address to store the input
    movq $0, %rax                     # Clear %rax for variadic function call
    call scanf 

    # Compute the exponent:
    # Move the input number (8 bytes) into %rdi as the base,
    # and move it into %rsi as the exponent.
    movq input_value(%rip), %rdi
    movq input_value(%rip), %rsi
    call exponent 

    # Display the result using printf.
    lea format_output(%rip), %rdi     # Output format string
    movq %rax, %rsi                   
    movq $0, %rax                     # Clear %rax for variadic function call
    call printf 

    # Ask the user whether to continue.
    lea continue_prompt(%rip), %rdi 
    call printf 

    # Read a single character from stdin using scanf with "%c"
    lea format_char(%rip), %rdi
    lea continue_flag(%rip), %rsi
    movq $0, %rax                     # Clear %rax for variadic function call
    call scanf 

    # Check if the input character is 'Y' or 'y'
    movzbl continue_flag(%rip), %eax   # Load the byte and zero extend it
    cmp $'Y', %al
    je .loop
    cmp $'y', %al
    je .loop

    # Exit the program.
    leave 
    ret
```

```sh
chan@CMA:~/C_Programming/Assembly$ ./test
Enter a number: 3
Exponent: 27
Type Y to keep going: y
Enter a number: 4
Exponent: 256
Type Y to keep going: n
chan@CMA:~/C_Programming/Assembly$ 
```

