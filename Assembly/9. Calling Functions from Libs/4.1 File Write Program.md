# File Write Program

```assembly
.section .note.GNU-stack, "", @progbits
.globl main

.section .data 
filename:
    .ascii "sonnet18.txt\0"
openmode:
    .ascii "w\0"
formatstring1: 
    .ascii "The age of %s is %d.\n\0"
sallyname: 
    .ascii "Sally\0"
sallyage:
    .quad 25
formatstring2:
    .ascii "%d and %d are %s's favorite numbers.\n\0"
joshname:
    .ascii "Josh\0"
joshfavoritefirst:
    .quad 7
joshfavoritesecond:
    .quad 13

.section .text 
main: 
    # create a stack frame with one local var
    # for the file pointer (aligned to 16 bytes)
    enter $16, $0 

    # open the file for writing
    movq $filename, %rdi
    movq $openmode, %rsi
    call fopen
    
    # save the file pointer in the local variable
    movq %rax, -8(%rbp)

    # write the first string 
    movq -8(%rbp), %rdi                # File pointer
    movq $formatstring1, %rsi         
    movq $sallyname, %rdx     

    # to pass the actual value to fprintf, we need to deference the .quad value using (%rip) addressing mode         
    movq sallyage(%rip), %rcx         
    movq $0, %rax                      # Clear %rax (for variadic functions)
    call fprintf 

    # write the second string
    movq -8(%rbp), %rdi                # File pointer
    movq $formatstring2, %rsi          
    movq joshfavoritefirst(%rip), %rdx 
    movq joshfavoritesecond(%rip), %rcx 
    movq $joshname, %r8                
    movq $0, %rax                      # Clear %rax (for variadic functions)
    call fprintf 

    # close the file 
    movq -8(%rbp), %rdi 
    call fclose 

    # return 
    movq $0, %rax 
    leave 
    ret
```

**Recall:**

- The **System V AMD64 calling convention** specifies the following registers for the first six arguments to a function:
  - `%rdi`: First argument
  - `%rsi`: Second argument
  - `%rdx`: Third argument
  - `%rcx`: Fourth argument
  - `%r8`: Fifth argument
  - `%r9`: Sixth argument

```sh
chan@CMA:~/C_Programming/Assembly$ gcc test.s -static -o test
test.s: Assembler messages:
test.s: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ./test
chan@CMA:~/C_Programming/Assembly$ 
```

`sonnet.txt`

```css
The age of Sally is 25.
7 and 13 are Josh's favorite numbers.
```

---