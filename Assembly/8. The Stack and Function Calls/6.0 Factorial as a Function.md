# Writing Factorial as a Function

`factorialfunc.s`

```assembly
.globl factorial
.section .text

factorial:
    # we will reserve space for 1 variable - the value we were called with
    # aligned to 16 bytes
    enter $16, $0

    # if the arg is 1, then return the result as 1.
    # otherwise, continue 
    cmpq $1, %rdi
    jne continue

    # return 1
    movq $1, %rax
    leave 
    ret 

continue:
    # save the arg into our stack storage
    movq %rdi, -8(%rbp)

    # call factorial with %rdi decreased by one
    decq %rdi
    call factorial 

    # after the recursive call, the result will be in %rax. Multiply the result by our first arg we stored on the stack
    mulq -8(%rbp)

    # result is in %rax which is what is needed for the return value

    leave 
    ret 
```

`runfactorial.s`

```assembly
.globl _start
.section .text 
_start: 
    # call factorial with 4
    movq $4, %rdi
    call factorial 

    movq %rax, %rdi
    movq $60, %rax 
    syscall 
```

```sh
chan@CMA:~/C_Programming/Assembly$ as factorialfunc.s -o factorialfunc.o
factorialfunc.s: Assembler messages:
factorialfunc.s:32: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ as runfactorial.s -o runfactorial.o
runfactorial.s: Assembler messages:
runfactorial.s:9: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld factorialfunc.o runfactorial.o -o runfactorial
chan@CMA:~/C_Programming/Assembly$ ./runfactorial
chan@CMA:~/C_Programming/Assembly$ echo $?
24
```

