# Using `.equ` to define Stack Frame Offsets

- As the number of local variables expands, it gets harder to remember which stack offset is used for which value.
- We can use `.equ` to give our stack offsets more clear names.

---

## Code Example

- Previous program with the only difference being that we defined `LOCAL_NUM` to refer to the stack offset that we are storing the number.

`factorialfunc.s`

```assembly
.globl factorial
.section .text

# this is the offset into the stack frame (%rbp) that we store the number for which we are taking the factorial.
.equ LOCAL_NUM, -8

factorial:
    # we will reserve space for 1 variable - the value we were called with
    # aligned to 16 bytes
    enter $16, $0

    # if the arg is 1, then return the result as 1.
    # otherwise, continue 
    cmpq $1, %rdi
    jne continue

    # return 1
    movq $1, %rax
    leave 
    ret 

continue:
    # save the arg into our stack storage
    movq %rdi, LOCAL_NUM(%rbp)

    # call factorial with %rdi decreased by one
    decq %rdi
    call factorial 

    # the result will be in %rax. Multiply the result by our first arg we stored on the stack
    mulq LOCAL_NUM(%rbp)

    # result is in %rax which is what is needed for the return value

    leave 
    ret 
```

`runfactorial.s`

```assembly
.globl _start
.section .text 
_start: 
    # call factorial with 4
    movq $5, %rdi
    call factorial 

    movq %rax, %rdi
    movq $60, %rax 
    syscall 
```

```sh
chan@CMA:~/C_Programming/Assembly$ as factorialfunc.s -o factorialfunc.o
factorialfunc.s: Assembler messages:
factorialfunc.s:35: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ as runfactorial.s -o runfactorial.o
runfactorial.s: Assembler messages:
runfactorial.s:9: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ld factorialfunc.o runfactorial.o -o runfactorial
chan@CMA:~/C_Programming/Assembly$ ./runfactorial
chan@CMA:~/C_Programming/Assembly$ echo $?
120
```

