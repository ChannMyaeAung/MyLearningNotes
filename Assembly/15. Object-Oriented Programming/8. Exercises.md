# Exercises

1. Create a Triangle and Rectangle class, each of which hold the base and height of the object. Then create a method called findArea which calculates the area of each. Create a Shape interface which has this method, and encode a vtable for it.

```assembly
.section .note.GNU-stack, "", @progbits # Mark stack as non-executable (security feature)
.section .data
rect_msg: .asciz "Rectangle area: %d\n"
tri_msg: .asciz "Triangle area: %d\n"

# vtables - each contains pointers to class-specific methods 
vtable_Rectangle:
    .quad rectangle_findArea  # pointer to the rectangle's area calculation method   

vtable_Triangle:
    .quad triangle_findArea      

# Rectangle object layout:
# [0]: pointer to vtable_Rectangle
# [1]: base
# [2]: height
rect_obj:
    .quad vtable_Rectangle
    .quad 10                     # base = 10
    .quad 5                      # height = 5      

# Triangle object layout:
# [0]: pointer to vtable_Triangle
# [1]: base
# [2]: height
tri_obj:
    .quad vtable_Triangle
    .quad 8                      # base = 8
    .quad 6                      # height = 6      

.section .text
.globl main
.extern printf 
main:
    # --- Call Rectangle.findArea(rect_obj) ---
    # 1. Load the vptr from the obj
    # 2. Dereference it to fetch the method address 
    # 3. Pass the obj's address in %rdi and call the method pointer 
    # This looks up and invokes the correct findArea at runtime, demonstrating polymorphism without hard-coding the class type.


    movq rect_obj(%rip), %rax # load vtable pointer from rect_obj into %rax   
    movq (%rax), %rbx  # Load the address of rectangle_findArea method into %rbx          
    leaq rect_obj(%rip), %rdi # Load address of rect_obj into %rdi as 'this' pointer    
    call *%rbx  # call the findArea method thru the vtable               

    # Result (area = 10*5 = 50) now in %rax

    # Print rectangle area
    movq %rax, %rsi # 2nd arg for printf: the area value (50)
    leaq rect_msg(%rip), %rdi # 1st arg for printf: format string  
    movq $0, %rax # no floating point args 
    call printf 

    # --- Call Triangle.findArea(tri_obj) ---
    movq tri_obj(%rip), %rax     
    movq (%rax), %rbx            
    leaq tri_obj(%rip), %rdi     
    call *%rbx                   
    # Result (area = (8*6)/2 = 24) now in %rax

    # Print triangle area
    movq %rax, %rsi 
    leaq tri_msg(%rip), %rdi 
    movq $0, %rax 
    call printf

    # Exit program
    movq $60, %rax               
    syscall

# ----------------------------
# Method Implementations
# 1. each individual method routines extract base and height from the obj's memory
# 2. perform the appropriate calculation
# 3. return the result in %rax 
# ----------------------------

rectangle_findArea:
    # Compute base * height
    movq 8(%rdi), %rax           # Load base value (offset=8 bytes from object start)
    imulq 16(%rdi), %rax         # Multiply by height (offset=16 bytes from object start)
    ret # Return with area in %rax

triangle_findArea:
    # Compute (base * height) / 2
    movq 8(%rdi), %rax           
    imulq 16(%rdi), %rax         
    shrq $1, %rax  # Shift right by 1 bit (divide by 2)
    ret

```

```sh
chan@CMA:~/C_Programming/Assembly$ gcc -no-pie run.s -o run
chan@CMA:~/C_Programming/Assembly$ ./run
Rectangle area: 50
Triangle area: 24
```

#### Object-Oriented Design Explanation

The code demonstrates polymorphism in assembly language by implementing:

1. **Virtual Method Tables (vtables)**: Each shape has its own vtable containing pointers to its specific implementation of methods.
2. **Object Structure**: Each object begins with a pointer to its class vtable, followed by its instance variables (base and height).
3. **Dynamic Method Dispatch**: Methods are called through the vtable, allowing different implementations to be selected at runtime:
   - `rectangle_findArea` calculates area as `base * height`.
   - `triangle_findArea` calculates area as `(base * height) / 2`.
4. **Memory Layout**:
   - `rect_obj` and `tri_obj` have identical memory layouts
   - Offset 0: vtable pointer (8 bytes)
   - Offset 8: base value (8 bytes)
   - Offset 16: height value (8 bytes)

---

