# Memory Pools

- Memory pools are like multiple areas of memory from which we can request allocations and deallocations.
- Imagine segmenting out our memory so that not only do we request an amount of memory we want, we also request which memory pool we want it allocated from.

---

## Code Example

- The program allows a user to specify a pool number to allocate from and also provides a `deallocate_pool` func to deallocate all allocations from a particular pool.
- It has an additional field on each allocation which is the pool number that the allocation belongs to.
- When deallocating a pool, the deallocator simply walks the list of memory allocations and deallocates each one that belongs to the pool.

`run.s`

```assembly
.section .note.GNU-stack, "", @progbits
.globl allocate, deallocate, deallocate_pool 

.section .data 
memory_start: 
    .quad 0 
memory_end: 
    .quad 0 

.section .text 
.equ HEADER_SIZE, 32 # only need 24, but this is a 16-byte aligned value 
.equ HDR_IN_USE_OFFSET, 0 
.equ HDR_SIZE_OFFSET, 8 
.equ HDR_POOL_OFFSET, 16 

.equ BRK_SYSCALL, 12 

# Register usage: 
# %r10 - memory pool 
# %rdx - size requested 
# %rsi - pointer to current memory being examined 
# %rcx - copy of memory_end 

allocate_init:
    # Find the program break.
    movq $0, %rdi 
    movq $BRK_SYSCALL, %rax 
    syscall 

    # The current break will be both the start and end of our memory 
    movq %rax, memory_start 
    movq %rax, memory_end 
    jmp allocate_continue 

allocate_move_break:
    # old break is saved in %r8 to return to user
    movq %rcx, %r8 

    # calculate where we want the new break to be
    # (old break + size )
    movq %rcx, %rdi 
    addq %rdx, %rdi 

    # save this value 
    movq %rdi, memory_end 

    # Tell Linux where the new break is 
    movq $BRK_SYSCALL, %rax 
    syscall 

    # address is in %r8 - mark size, availability, and pool
    movq $1, HDR_IN_USE_OFFSET(%r8) # mark it as in use 
    movq %rdx, HDR_SIZE_OFFSET(%r8)
    movq %r10, HDR_POOL_OFFSET(%r8)

    # Actual return value is beyond our header
    addq $HEADER_SIZE, %r8 
    movq %r8, %rax 
    ret 

    # this version of allocate has 
    # two params - pool and size

allocate:
    # save the pool number in r10 
    movq %rdi, %r10 

    # save the amount requested into %rdx 
    movq %rsi, %rdx 

    # actual amount needed is actually larger 
    addq $HEADER_SIZE, %rdx

    # if we haven't initialized, do so
    cmpq $0, memory_start 
    je allocate_init 

allocate_continue:
    movq memory_start, %rsi 
    movq memory_end, %rcx 

allocate_loop: 
    # if we have reached the end of memory
    # we have to allocate new memory
    # by moving the break 
    cmpq %rsi, %rcx 
    je allocate_move_break 

    # is the next block available?
    cmpq $0, HDR_IN_USE_OFFSET(%rsi)
    jne try_next_block 

    # is the next block big enough 
    cmpq %rdx, HDR_SIZE_OFFSET(%rsi)
    jb try_next_block 

    # this block is great!
    # mark it as in use
    movq $1, HDR_IN_USE_OFFSET(%rsi)

    # set the pool 
    movq %r10, HDR_POOL_OFFSET(%rsi)

    # move beyond the header
    addq $HEADER_SIZE, %rsi 

    # Return the value 
    movq %rsi, %rax 
    ret 

try_next_block: 
    # this block didn't work, move to the next one
    addq HDR_SIZE_OFFSET(%rsi), %rsi 
    jmp allocate_loop 

deallocate:
    # free is simple - just mark the block as free
    movq $0, HDR_IN_USE_OFFSET - HEADER_SIZE(%rdi)
    movq $0, HDR_POOL_OFFSET - HEADER_SIZE(%rdi)
    ret 

deallocate_pool: 
    # %rdi has the pool number 
    # walk the allocations and deallocate 
    # anything with the pool num 
    movq memory_start, %rsi 
    movq memory_end, %rcx 

deallocate_pool_loop: 
    cmpq %rsi, %rcx 
    je deallocate_loop_complete 

    cmpq %rdi, HDR_POOL_OFFSET(%rsi)
    je deallocate_from_pool
    addq HDR_SIZE_OFFSET(%rsi), %rsi 
    jmp deallocate_pool_loop 

deallocate_from_pool:
    movq $0, HDR_POOL_OFFSET(%rsi)
    movq $0, HDR_IN_USE_OFFSET(%rsi)
    addq HDR_SIZE_OFFSET(%rsi), %rsi 
    jmp deallocate_pool_loop 

deallocate_loop_complete: 
    ret 
```

`run.c`

```c
#include <stdio.h>
void *allocate(int pool, int size);
void deallocate(void *ptr);
void deallocate_pool(int pool);

int main()
{
    // get 400 bytes from pool 1
    char *a1 = allocate(1, 400);

    // get 32 bytes from pool 2
    char *a2 = allocate(2, 32);

    // get 80 bytes from pool 2
    char *ae = allocate(2, 80);

        // release all of pool 2
    deallocate_pool(2);

    fprintf(stdout, "Allocations: %p %p %p\n", a1, a2, ae);

    // Test if pool 1 still works
    char *a3 = a1; // save pointer to verify it's still usable

    deallocate(a1);
    fprintf(stdout, "Allocations: %p %p %p\n", a1, a2, a3);

    return 0;
}
```



```sh
chan@CMA:~/C_Programming/Assembly$ gcc -no-pie run.s run.c -o run
run.s: Assembler messages:
run.s:144: Warning: end of file not at end of a line; newline inserted
chan@CMA:~/C_Programming/Assembly$ ./run
Allocations: 0x15cac020 0x15cac1d0 0x15cac210
Allocations: 0x15cac020 0x15cac1d0 0x15cac020
```

- We can see the the in the second print line, `a3 = a1`, so they show the same address (`0x15cac020`).
