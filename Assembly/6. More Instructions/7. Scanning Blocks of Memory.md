# Scanning Blocks of Memory

- `scasq` uses `%rdi` and  loads the value in the address specified by `%rdi` and compares it to the value in `%rax`.
- And automatically increments (or decrements) `%rdi` based on the Direction Flag.
- With the `repne`prefix, it repeatedly scans until either a match is found (Zero Flag is set) or the counter in `%rcx` reaches zero.

---

## Code Example

```assembly
.section .data
target_value:
    .quad 0xDEADBEEFDEADBEEF    

memory_block:
    .quad 0x1111111111111111, 0x2222222222222222, 0xDEADBEEFDEADBEEF, 0x3333333333333333
num_elements:
    .quad 4                    # Number of quadwords in memory_block

.section .text
.globl _start
_start:
    # Load target value into %rax.
    movq target_value(%rip), %rax

    # Load address of memory_block into %rdi.
    leaq memory_block(%rip), %rdi

    # Load number of elements into %rcx.
    movq num_elements(%rip), %rcx

    # Clear DF to ensure forward scanning.
    cld

    # Scan memory block: repne scasq compares %rdi with %rax, 
    # and increments %rdi by 8 (quadword size) on each iteration.
    repne scasq

    # After repne scasq, %rcx holds the remaining count.
    # If a match is found, Zero Flag is set; if not, %rcx becomes 0.
    jnz not_found   # Jump if Zero Flag is not set (i.e. no match found).

    # If found, compute the index:
    # The original count is in num_elements; repne scasq decrements %rcx.
    # The index of the found element is: index = original_count - %rcx - 1.
    movq num_elements(%rip), %rdx  # Load original count into %rdx.
    subq %rcx, %rdx              # Subtract remaining count.
    dec %rdx                    # Adjust for the extra increment.
found:
    # Return the index as exit code.
    movq %rdx, %rdi             # Exit code = index.
    movq $60, %rax              
    syscall

not_found:
    # No match was found: return -1.
    movq $-1, %rdi
    movq $60, %rax
    syscall
```

```sh
chan@CMA:~/C_Programming/Assembly$ as test.s -o test.o
chan@CMA:~/C_Programming/Assembly$ ld test.o -o test
chan@CMA:~/C_Programming/Assembly$ ./test
chan@CMA:~/C_Programming/Assembly$ echo $?
2
```

