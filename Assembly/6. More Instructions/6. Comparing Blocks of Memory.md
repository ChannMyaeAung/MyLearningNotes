# Comparing Blocks of Memory

- `cmpsq` compares blocks of memory. (64 bits)
  - **`cmpsb`:** Compare byte (8 bits)
  - **`cmpsw`:** Compare word (16 bits)
  - **`cmpsd/cmpsl`:** Compare doubleword (32 bits)
- It will load values from the addresses listed in `%rsi` and `%rdi`, compare them, set the flags appropriately, and advance `%rsi` and `%rdi` based on the direction flag (DF).
- Can be prefixed by the `rep` to use `%rcx` as a counter. (or `repe`/ `repz` if we care about the zero flag)
  - E.g With `repe cmpsq`, the processor will compare quadwords repeatedly as long as they are equal and the counter (in RCX) is not zero.

---

## Code Example 

- Comparing two blocks byte-by-byte

```assembly
.section .data
block1:
    .asciz "Hello, world!"
block2:
    .asciz "Hello, world"
length:
    .quad 13         # Length of the blocks (excluding null terminator)

.section .text
.globl _start
_start:
    # Load the addresses of the two blocks into RSI and RDI.
    leaq block1(%rip), %rsi
    leaq block2(%rip), %rdi

    # Load the number of bytes to compare into RCX.
    movq length(%rip), %rcx

    # Clear the Direction Flag to ensure forward movement.
    cld

    # Compare the blocks using CMPSB with REPNE or REPE.
    repe cmpsb       # Repeats while bytes are equal (ZF stays set)
    
    # At this point:
    # - If RCX is 0, all bytes compared were equal.
    # - If a mismatch occurred, the Zero Flag is cleared.
    jne blocks_not_equal

blocks_equal:
    # Blocks are identical. For example, exit with status 0.
    movq $60, %rax
    xor %rdi, %rdi      # exit code 0
    syscall

blocks_not_equal:
    # Blocks differ. Exit with a non-zero status (e.g., 1).
    movq $60, %rax
    movq $1, %rdi       # exit code 1
    syscall
```

```sh
chan@CMA:~/C_Programming/Assembly$ as test.s -o test.o
chan@CMA:~/C_Programming/Assembly$ ld test.o -o test
chan@CMA:~/C_Programming/Assembly$ ./test
chan@CMA:~/C_Programming/Assembly$ echo $?
1
```

